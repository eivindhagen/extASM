
; >-------------------------------------------------------------<

.MacroScanner
   STMFD    R13!,{R14}

   BL       Wimp_Text_AddBlankLine

   ADR      R0,Macro_WTLine_1
   BL       Wimp_Text_AddLine

   BL       Setup_MacroScanner

   LDR      R0,Options
   TST      R0,#Option_UseDefaultHeader
   BLNE     MacroScan_Default_Header

   MOV      R0,#Flex_Source
   BL       MacroScan_Source

   ADR      R0,Macro_WTLine_2
   LDR      R2,Macro_Num
   BL       Wimp_Text_AddLine

   BL       Wimp_Text_Window

   LDMFD    R13!,{R15}

.Macro_WTLine_1   DCB   "MacroScanning Source",0
.Macro_WTLine_2   DCB   "Counting macros : %d",0
   ALIGN

; >-------------------------------------------------------------<

.Setup_MacroScanner
   MOV      R0,#0
   STR      R0,Brace_Count
   STR      R0,Enable_Brace_Return
   STR      R0,Process_Macro_Level
   STR      R0,Source_Count_Offset
   STR      R0,Source_Current_Offset
   STR      R0,Source_Line_Num
   MOV      R0,#2
   STR      R0,extASM_Doing
   MOV      R15,R14

; >-------------------------------------------------------------<

.MacroScan_Default_Header
   STMFD    R13!,{R14}
   ADR      R0,MS_Header_End
   ADR      R1,Header_Filename
   LDR      R2,Header_Leafname
   MOV      R3,#1
   BL       Setup_Error_Context

   MOV      R0,#Flex_Header
   BL       MacroScan_Source

.MS_Header_End
   MOV      R0,#0
   BL       Restore_Error_Context
   LDMFD    R13!,{R15}

; >-------------------------------------------------------------<

.MacroScan_Source                      ;_R0 = Flex block for source
   STMFD    R13!,{R14}
   BL       Flex_Adr
   MOV      R10,R0                     ; R10-> Source line 1

.Macro_Line_Loop
   LDRB     R8,[R10],#1                ; R8 = Source Char
   LDR      R0,Options
   TST      R0,#Option_NoPeriod
   BLEQ     Skip_Blanks

   CMP      R8,#&0A                    ; NewLine -> Quit scan
   CMPNE    R8,#&00                    ; NULL -> Quit scan
   BLNE     MacroScan_Token

   LDR      R0,Poll_Timer
   SUBS     R0,R0,#1
   LDRLE    R0,Poll_Lines
   STR      R0,Poll_Timer
   BLLE     Wimp_Text_Window

   LDR      R0,Source_Line
   ADD      R0,R0,#1
   STR      R0,Source_Line

   CMP      R8,#&00                    ; NULL -> End of source
   BNE      Macro_Line_Loop
   LDMFD    R13!,{R15}

; >-------------------------------------------------------------<

.MacroScan_Token                       ;_R8 = First char of token, ~R8 = EOL
   STMFD    R13!,{R14}

.MacroScan_Token_Direct                ;_R0 = Options
   ADR      R1,MS_LineEnd
   ADR      R14,Error_Context
   STMIA    R14,{R1,R4-R13}            ; Store Asm_Error context

   CMP      R8,#&01
   BNE      MTD_Not_Source_Start
   BL       Source_Start
   B        MS_End

.MTD_Not_Source_Start
   CMP      R8,#&02
   BNE      MTD_Not_Source_End
   BL       Source_End
   B        MS_End

.MTD_Not_Source_End
   CMP      R8,#' '
   CMPNE    R8,#&09
   MOVEQ    R0,#0
   BLEQ     Skip_Blanks

   CMP      R8,#':'
   BEQ      MS_Colon
   CMP      R8,#';'
   BNE      MTD_Not_Flush_Line
   BL       Flush_Line
   LDMFD    R13!,{R15}

.MTD_Not_Flush_Line
   CMP      R8,#'{'
   CMPNE    R8,#'}'
   BNE      MS_Not_Brace

   LDR      R1,Brace_Count
   CMP      R8,#'{'
   ADDEQ    R1,R1,#1
   CMP      R8,#'}'
   SUBEQ    R1,R1,#1
   LDR      R2,Enable_Brace_Return
   CMP      R1,#0
   RSBLTS   R3,R2,#0
   MOVLT    R8,#&00
   LDMLTFD  R13!,{R15}
   STR      R1,Brace_Count

.MS_Not_Brace
   CMP      R8,#'M'                    ; [M]ACRO
   CMPNE    R8,#'m'                    ; [m]acro
   TSTEQ    R0,#Option_NoPeriod
   BEQ      MS_Macro

   B        MS_LineEnd

; >-------------------------------------------------------------<

.MS_Colon                              ;_R8 = Colon char
   LDRB     R8,[R10],#1
   LDR      R0,Options
   TST      R0,#Option_NoPeriod
   BLEQ     Skip_Blanks
   CMP      R8,#&0A
   BNE      MacroScan_Token_Direct
   LDMFD    R13!,{R15}

; >-------------------------------------------------------------<

.MS_Macro                              ;_R8 = "M"
   LDRB     R8,[R10],#1
   BIC      R8,R8,#32
   CMP      R8,#'A'
   BNE      MS_LineEnd
   LDRB     R8,[R10],#1
   BIC      R8,R8,#32
   CMP      R8,#'C'
   BNE      MS_LineEnd
   LDRB     R8,[R10],#1
   BIC      R8,R8,#32
   CMP      R8,#'R'
   BNE      MS_LineEnd
   LDRB     R8,[R10],#1
   BIC      R8,R8,#32
   CMP      R8,#'O'
   BNE      MS_LineEnd

   ;------> "MACRO"
   STMFD    R13!,{R4-R6}
   MOV      R4,R10
   LDR      R5,Source_Line
   ADR      R0,MacroScan_Token
   MOV      R1,#0
   BL       Process_Macro
   CMP      R8,#&00
   LDMEQFD  R13!,{R4-R6,R15}
   CMN      R14,#1
   LDMEQFD  R13!,{R4-R6}
   BEQ      MS_End

   STMFD    R13!,{R8,R10}
   MOV      R10,R4
   LDRB     R8,[R10],#1
   BL       Skip_Blanks
   ADR      R4,Block
   MOV      R0,R4
   SUB      R10,R10,#1
   BL       StrCopySource

   ;------> Check if macro is already registered, should NOT occur
   ADR      R0,MSMACRO_Register
   LDR      R1,Source_Filename
   LDR      R2,Source_LeafName
   MOV      R3,R5
   BL       Setup_Error_Context
   MOV      R0,R4
   BL       Find_Macro
   CMN      R14,#1
   BEQ      MSMACRO_Not_Register
   MOV      R6,R0
   CMN      R1,#1
   BEQ      MSMACRO_Register
   MOV      R0,#&54                    ; Asm_Error #&54 = Redefinition of macro, '<Name>'
   MVN      R1,#0
   MOV      R2,R4
   BL       Asm_Error_Return

.MSMACRO_Not_Register
   MOV      R0,#0
   BL       Restore_Error_Context
   LDMFD    R13!,{R8,R10}
   LDMFD    R13!,{R4-R6}
   B        MS_LineEnd

.MSMACRO_Register
   ;------> Register new macro
   LDR      R1,Previous_Macro
   CMP      R1,#0
   STREQ    R6,First_Macro
   STRNE    R6,[R1,#16]
   STR      R6,Previous_Macro
   SUB      R1,R10,#1
   LDR      R2,Source_Filename
   STMIA    R6,{R1,R2,R5}
   MOV      R0,R4
   ADD      R1,R6,#20
   BL       StrCopy

   MOV      R0,#0
   BL       Restore_Error_Context
   LDMFD    R13!,{R8,R10}
   LDMFD    R13!,{R4-R6}
   B        MS_End

.MS_LineEnd
   BL       Flush_Line
   LDMFD    R13!,{R15}

.MS_End
   BL       Skip_Blanks
   CMP      R8,#':'
   BEQ      MS_Colon
   CMP      R8,#';'
   BNE      MSE_Not_Flush_Line
   BL       Flush_Line
   LDMFD    R13!,{R15}
.MSE_Not_Flush_Line
   CMP      R8,#&0A
   LDMEQFD  R13!,{R15}
   MOV      R0,#&24                    ; Asm_Error #&24 = Syntax overflow (too many operands)
   B        Asm_Error
